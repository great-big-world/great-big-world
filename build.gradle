plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
}

repositories {
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.minecraftforge.net/' }
    maven { url "https://maven.terraformersmc.com/releases/" }
}

version = project.mod_version
group = project.maven_group
archivesBaseName = "${name}-${project.minecraft_version}"

loom {
    accessWidenerPath = file("src/main/resources/great_big_world.accesswidener")

    runs {
        datagenClient {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"
            runDir "build/datagen"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "com.github.glitchfiend:TerraBlender-fabric:${minecraft_version}-${project.terrablender_version}"
    modApi "com.terraformersmc:modmenu:${project.mod_menu_version}"
    modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)
}

java {
    withSourcesJar()
}
